import "@cadl-lang/rest";

using Cadl.Http;

model Pet {
  name: string;
}

model Feline extends Pet {
  meows: boolean;
  hisses: boolean;
}

model Cat extends Feline {
  likesMilk: boolean;
}

model Kitten extends Cat {
  eatsMiceYet: boolean;
}

model Horse extends Pet {
  isAShowHorse: boolean;
}

@doc("Unexpected error")
model ErrorResponse {
  @body body: string;
}

@route("/multipleInheritance")
namespace MultipleInheritance {
  @doc("Get a horse with name 'Fred' and isAShowHorse true")
  @get("horse")
  op getHorse(): OkResponse<Horse> | ErrorResponse;

  @doc("Put a horse with name 'General' and isAShowHorse false")
  @put("horse")
  op putHorse(@body horse: Horse): OkResponse<string> | ErrorResponse;

  @doc("Get a pet with name 'Peanut'")
  @get("pet")
  op getPet(): OkResponse<Pet> | ErrorResponse;

  @doc("Put a pet with name 'Butter'")
  @put("pet")
  op putPet(@body horse: Pet): OkResponse<string> | ErrorResponse;

  @doc("Get a feline where meows and hisses are true")
  @get("feline")
  op getFeline(): OkResponse<Feline> | ErrorResponse;

  @doc("Put a feline who hisses and doesn't meow")
  @put("feline")
  op putFeline(@body feline: Feline): OkResponse<string> | ErrorResponse;

  @doc("Get a cat with name 'Whiskers' where likesMilk, meows, and hisses is true")
  @get("cat")
  op getCat(): OkResponse<Cat> | ErrorResponse;

  @doc("Put a cat with name 'Boots' where likesMilk and hisses is false, meows is true")
  @put("cat")
  op putCat(@body cat: Cat): OkResponse<string> | ErrorResponse;

  @doc(
    "Get a kitten with name 'Gatito' where likesMilk and meows is true, and hisses and eatsMiceYet is false"
  )
  @get("kitten")
  op getKitten(): OkResponse<Kitten> | ErrorResponse;

  @doc(
    "Put a kitten with name 'Kitty' where likesMilk and hisses is false, meows and eatsMiceYet is true"
  )
  @put("kitten")
  op putKitten(@body kitten: Kitten): OkResponse<string> | ErrorResponse;
}
